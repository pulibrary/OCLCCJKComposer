'MacroName:ComposeHangulSingleField
'MacroDescription:Compose hangul from individual jamo in a single field

Declare Function GenerateHangulHTML(iHangulCode As Long) As String

Sub Main
   
   Dim CS As Object
On Error Resume Next
   Set CS = GetObject(,"Connex.Client")
On Error GoTo 0
   If CS Is Nothing Then
      Set CS = CreateObject("Connex.Client")
   End If
   
   bool = CS.GetFieldLineUnicode(CS.CursorRow, sField)
   If bool = FALSE Then
      MsgBox "Not viewing a MARC record. Exiting..."
      Exit Sub
   End If


   'Make sure field contains decomposed Korean jamo
   If InStr(1, sField, "&#x11") = 0 Then
      MsgBox "Field contains no Korean jamo. Exiting..."
      Exit Sub
   End If
   
   sNewField = ""
   
   iLen = Len(sField)
   
   iHangulStage = 0
   iHangulCode = 0
   
   For i = 1 To iLen
      If i > iLen - 8 Then
         sNewField = sNewField & Mid(sField,i,1)
      Else
         If Mid(sField,i,5) = "&#x11" Then
            sCodepoint = Mid(sField,i+3,4)
            iCode = Val("&H" & sCodepoint & "&")  
            If (iHangulStage = 0) Or (iHangulStage > 1 And iCode - 4519 < 0) Then
               If iHangulCode > 0 Then
                  sNewField = sNewField & GenerateHangulHTML(iHangulCode)
                  iHangulCode = 0
               End If
               iCode = (iCode - 4352)*588
               iHangulCode = iHangulCode + iCode
               iHangulStage = 1
            ElseIf iHangulStage = 1 Then
               iCode = (iCode - 4449)*28
               iHangulCode = iHangulCode + iCode
               iHangulStage = 2
            Else
               iCode = iCode - 4519
               iHangulCode = iHangulCode + iCode
               iHangulStage = 0
            End If
            i = i + 7
            If i = iLen-1 Then
               sNewField = sNewField & GenerateHangulHTML(iHangulCode)
            End If
         Else
           If iHangulCode > 0 Then
               sNewField = sNewField & GenerateHangulHTML(iHangulCode)
               iHangulCode = 0
           End If
           sNewField = sNewField & Mid(sField,i,1) 
         End If
      End If
   Next i
   
   CS.SetFieldLine CS.CursorRow, sNewField   
    
End Sub

Function GenerateHangulHTML(iHangulCode As Long) As String
   iHangulCode = iHangulCode + 44032
   GenerateHangulHTML = "&#x" & Hex(iHangulCode) & ";"
End Function
